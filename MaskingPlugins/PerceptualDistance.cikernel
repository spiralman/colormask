/*
A Core Image Kernel that plots the distance to a specified color, within
a tolerance, using the CIE Lab perceptual color space.

Colors are mapped to the CIE Lab color space, which measures a perceptual
color and luminance. Pixels which are within a specified distance of the
color (chromaTolerance) and luminance (luminanceTolerance) are black. All
other pixels fade to white at a rate controlled by falloff.

Distances are normalized to 0.0-1.0
*/

vec3 rgb_to_lab( __color color )
{
	vec3 over = pow((color.rgb + 0.055) / 1.055,vec3(2.4));
	vec3 under = color.rgb / 12.92;
	
	vec3 scaled_rgb = compare( color.rgb - 0.04045, under, over ) * vec3(100);
	
	vec3 xyz = vec3((scaled_rgb.r * 0.4124 + scaled_rgb.g * 0.3576 + scaled_rgb.b * 0.1805) / 95.047,
					(scaled_rgb.r * 0.2126 + scaled_rgb.g * 0.7152 + scaled_rgb.b * 0.0722) / 100.000,
					(scaled_rgb.r * 0.0193 + scaled_rgb.g * 0.1192 + scaled_rgb.b * 0.9505) / 108.883);
	
	over = pow(xyz,vec3(1.0/3.0));
	under = (xyz * 7.787) + vec3(16/116);
	vec3 lab = compare(xyz-0.008856,under,over);
	
	lab = vec3( ( 116.0 * lab.y ) - 16.0,
				500.0 * ( lab.x - lab.y ),
				200.0 * ( lab.y - lab.z ));
	
	return lab / 100.0;
}

kernel vec4 PerceptualDistance(sampler image, __color color, float chromaTolerance, float luminanceTolerance, float falloff)
{
	vec4 sample_color = sample(image,samplerCoord(image));
	
	vec3 sample_lab = rgb_to_lab(sample_color);
	vec3 sel_lab = rgb_to_lab(color);
	
	float cDist = abs(distance(sample_lab.yz,sel_lab.yz)) / 1.41;
	float lDist = abs(sample_lab.x - sel_lab.x);
	
	float cFalloff = ((cDist/(1.0-chromaTolerance)) - (chromaTolerance/(1.0-chromaTolerance))) / falloff;
	float lFalloff = ((lDist/(1.0-luminanceTolerance)) - (luminanceTolerance/(1.0-luminanceTolerance))) / falloff;
	
	float cClipped = compare(cFalloff, 0.0, cFalloff);
	float lClipped = compare(lFalloff, 0.0, lFalloff);
	
	float combined = cClipped + lClipped / 2.0;
	return vec4(combined, combined, combined,1.0);
}
